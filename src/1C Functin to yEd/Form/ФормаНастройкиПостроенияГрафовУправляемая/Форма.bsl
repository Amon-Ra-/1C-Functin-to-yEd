

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РабочийКаталог = Параметры.РабочийКаталог;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ПопробоватьНайтиGraphviz() Тогда
		Предупреждение("Не удалось найти установленную версию graphviz!");
	КонецЕсли;
	
КонецПроцедуры



///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура РабочийКаталогНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Если Диалог.Выбрать() Тогда
		РабочийКаталог = Диалог.Каталог;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКПрограммеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.ПолноеИмяФайла = ПутьКПрограмме;
	Диалог.Фильтр = "dot.exe|dot.exe";
	Если Диалог.Выбрать() Тогда
		ПутьКПрограмме = Диалог.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	// Выполним проверки
	Если ПустаяСтрока(РабочийКаталог) Тогда
		Предупреждение("Не указан рабочий каталог!");
		Возврат;
	ИначеЕсли Не СуществуетФайлИлиКаталог(РабочийКаталог, Ложь) Тогда
		Предупреждение("Не найден рабочий каталог: " + РабочийКаталог);
		Возврат;
	ИначеЕсли ПустаяСтрока(ПутьКПрограмме) Тогда
		Предупреждение("Не указан путь к программе graphviz!");
		Возврат;
	ИначеЕсли Не СуществуетФайлИлиКаталог(ПутьКПрограмме, Истина) Тогда
		Предупреждение("Не найдена программа: " + ПутьКПрограмме);
		Возврат;
	КонецЕсли;

	Если Прав(РабочийКаталог, 1) <> "\" И Прав(РабочийКаталог, 1) <> "/" Тогда
		РабочийКаталог = РабочийКаталог + "\";
	КонецЕсли;
	
	ИсполняемыйФайл = РабочийКаталог + "graph.bat";
	СтрФайла = ПолучитьТекстBATФайла();
	
	Текст = Новый ТекстовыйДокумент;
	Текст.ДобавитьСтроку(СтрФайла);
	Текст.Записать(ИсполняемыйФайл, КодировкаТекста.OEM);
	
	Закрыть(РабочийКаталог);
	
КонецПроцедуры



///////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Функция ПопробоватьНайтиGraphviz()
	
	ИмяКаталога = "C:\";
	Если Не НайтиФайлИлиКаталог("Program Files", ИмяКаталога, Истина) Тогда
		Возврат Ложь;
	ИначеЕсли Не НайтиФайлИлиКаталог("Graphviz*", ИмяКаталога, Истина) Тогда
		Возврат Ложь;
	ИначеЕсли Не НайтиФайлИлиКаталог("bin", ИмяКаталога, Истина) Тогда
		Возврат Ложь;
	ИначеЕсли Не НайтиФайлИлиКаталог("dot.exe", ИмяКаталога, Ложь) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПутьКПрограмме = ИмяКаталога;
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция НайтиФайлИлиКаталог(Шапка, ИмяКаталога, ИщемКаталог)
	
	Файлы = НайтиФайлы(ИмяКаталога, Шапка);
	спсИмен = Новый СписокЗначений; 
	Для Каждого Файл Из Файлы Цикл
		Если Файл.ЭтоКаталог() = ИщемКаталог И Файл.Существует() Тогда
			спсИмен.Добавить(Файл.ПолноеИмя);
		КонецЕсли;
	КонецЦикла;
	
	Если спсИмен.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	спсИмен.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	ИмяКаталога = спсИмен[0].Значение;
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция СуществуетФайлИлиКаталог(ПолноеИмя, ЭтоФайл)
	
	Файл = Новый Файл(ПолноеИмя);
	Возврат Файл.Существует() И ((Файл.ЭтоФайл() И ЭтоФайл) Или (Файл.ЭтоКаталог() И Не ЭтоФайл));
		
КонецФункции

&НаСервере
Функция ПолучитьТекстBATФайла()
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ИсходящийФайл",   РабочийКаталог + "graph.png");
	ДанныеЗаполнения.Вставить("ПутьКПрограмме",  ПутьКПрограмме);
	ДанныеЗаполнения.Вставить("ФорматФайла",     "png");
	ДанныеЗаполнения.Вставить("ТекстовыйФайл",   РабочийКаталог + "graph.txt");
	
	ЭтаОбработка = РеквизитФормыВЗначение("Объект");
	
	СтрФайла = ЭтаОбработка.ПолучитьМакет("ШаблонФайлаПостроениеГрафа").ПолучитьТекст();
	Для Каждого Элемент Из ДанныеЗаполнения Цикл
		Значение = Элемент.Значение;
		Если Найти(Значение, " ") <> 0 Тогда
			Значение = """" + Значение + """";
		КонецЕсли;
		
		СтрФайла = СтрЗаменить(СтрФайла, "[" + Элемент.Ключ + "]", Значение);
	КонецЦикла;
	
	Возврат СтрФайла;
	
КонецФункции

